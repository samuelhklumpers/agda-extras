{-# OPTIONS --guardedness #-}


module Examples where

open import Relation.Binary.PropositionalEquality
open import Data.Vec as V
open import Level
open import Data.Vec.Properties
open import Data.Nat renaming (suc to succ)
open import Data.Fin using (Fin)
open import Data.Unit
open import Effect.Applicative.Indexed

open import Functors
open import Applicatives
open import Monads
open import Representables
open import Extensionality


private
  variable
    ℓ : Level
    A B : Set ℓ


module VecEx where
  Vec' : ℕ → Set ℓ → Set ℓ 
  Vec' n A = Vec A n


  instance
    VecRep : ∀ {n ℓ} → Representable {ℓ = ℓ} (Vec' n)
    VecRep {n} = record {
      Rep = Fin n ;
      iso = λ A → record {
        to = lookup ;
        from = V.tabulate ;
        retract = λ v → tabulate∘lookup v ;
        section = section' } }
          where
            section' : {A : Set ℓ} → (f : Fin n → A) → lookup (V.tabulate f) ≡ f
            section' f = f-ext (λ i → lookup∘tabulate f i)

    VecMon :  ∀ {ℓ n} → IMonad {I = ⊤} (λ _ _ → Vec' {ℓ} n)
    VecMon {_} {n} = repToMon (Vec' n)

    VecApp :  ∀ {ℓ n} → IApplicative {I = ⊤} (λ _ _ → Vec' {ℓ} n)
    VecApp {_} {n} = monToApp (λ _ _ → Vec' n)

    VecFun :  ∀ {ℓ n} → Functor (Vec' {ℓ} n)
    VecFun {_} {n} = appToFun (λ _ _ → Vec' n)

  ex1 : Vec ℕ 2
  ex1 = (_+ 1) <$> 0 ∷ 1 ∷ []

  test1 : ex1 ≡ 1 ∷ 2 ∷ []
  test1 = refl
